#include <Arduino.h>
#include <ESP32Servo.h>

// Servos
Servo motorCima;
Servo motorMola1;
Servo motorMola2;
Servo motorMola3;
Servo motorBaixo;

// Pinos dos servos
#define MOTOR_CIMA_PIN   27
#define MOTOR_MOLA1_PIN  13
#define MOTOR_Mola2_PIN  12
#define MOTOR_MOLA3_PIN  14
#define MOTOR_BAIXO_PIN  26

// Estados possiveis
typedef enum {
  ESTADO_RELAXADO,
  PRESO_DISTENDIDO,
  SOBE_1,
  DESCE_1,
  PRESO_CONTRAIDO,
  SOBE_2,
  DESCE_2
} Estado;

// Estado atual
Estado estado = PRESO_DISTENDIDO;

// Controle do ciclo
bool cicloSubida = false;
bool cicloDescida = false;

const char* nomeEstado(Estado e) {
  switch (e) {
    case ESTADO_RELAXADO: return "RELAXADO";
    case PRESO_DISTENDIDO: return "PRESO_DISTENDIDO";
    case SOBE_1: return "SOBE_1";
    case DESCE_1: return "DESCE_1";
    case PRESO_CONTRAIDO: return "PRESO_CONTRAIDO";
    case SOBE_2: return "SOBE_2";
    case DESCE_2: return "DESCE_2";
    default: return "DESCONHECIDO";
  }
}

void setServoPosicao(Servo &servo, bool ativo) {
  if (ativo) servo.write(90);
  else servo.write(0);
}

// Move as 3 molas simultaneamente
void setServoContinuo(int v1, int v2, int v3, unsigned long duracao) {
  // 1. Manda os 3 começarem a girar
  motorMola1.write(v1);
  motorMola2.write(v2);
  motorMola3.write(v3);
  
  // 2. Espera o tempo definido (os 3 estão girando agora juntos)
  delay(duracao); 
  
  // 3. Manda os 3 pararem (90 = neutro)
  motorMola1.write(90); 
  motorMola2.write(90);
  motorMola3.write(90);
}

void aplicaEstado(Estado e) {
  Serial.print("Novo estado: ");
  Serial.println(nomeEstado(e));
  switch (e) {
    case ESTADO_RELAXADO:
      setServoPosicao(motorCima, false);
      setServoPosicao(motorBaixo, false);
      setServoContinuo(90, 90, 90, 0);
      break;

    case PRESO_DISTENDIDO:
      setServoPosicao(motorCima, true);
      setServoPosicao(motorBaixo, true);
      setServoContinuo(90, 90, 90, 100); 
      break;

    case SOBE_1:
      setServoPosicao(motorCima, true);
      setServoPosicao(motorBaixo, false);
      setServoContinuo(0, 0, 0, 2000); 
      break;

    case DESCE_1:
      setServoPosicao(motorCima, false);
      setServoPosicao(motorBaixo, true);
      setServoContinuo(0, 0, 0, 2000);
      break;

    case PRESO_CONTRAIDO:
      setServoPosicao(motorCima, true);
      setServoPosicao(motorBaixo, true);
      setServoContinuo(90, 90, 90, 100);
      break;

    case SOBE_2:
      setServoPosicao(motorCima, false);
      setServoPosicao(motorBaixo, true);
      setServoContinuo(180, 180, 180, 2000);
      break;

    case DESCE_2:
      setServoPosicao(motorCima, true);
      setServoPosicao(motorBaixo, false);
      setServoContinuo(180, 180, 180, 2000);
      break;
  }
}

// Logica comum de pausa
Estado pausaEstado(Estado e) {
  if (e == SOBE_1 || e == DESCE_1) return PRESO_CONTRAIDO;
  else if (e == SOBE_2 || e == DESCE_2) return PRESO_DISTENDIDO;
  else return e;
}

// Inversao imediata de direcao
void inverterDirecao(bool indoParaCima) {
  if (indoParaCima && cicloDescida) {
    if (estado == DESCE_1) estado = PRESO_CONTRAIDO;
    else if (estado == DESCE_2) estado = PRESO_DISTENDIDO;
  }
  else if (!indoParaCima && cicloSubida) {
    if (estado == SOBE_1) estado = PRESO_CONTRAIDO;
    else if (estado == SOBE_2) estado = PRESO_DISTENDIDO;
  }
  aplicaEstado(estado);
}

void setup() {
  Serial.begin(115200);

  motorCima.attach(MOTOR_CIMA_PIN);
  motorMola1.attach(MOTOR_MOLA1_PIN);
  motorMola2.attach(MOTOR_Mola2_PIN);
  motorMola3.attach(MOTOR_MOLA3_PIN);
  motorBaixo.attach(MOTOR_BAIXO_PIN);

  aplicaEstado(estado);
  Serial.println("Digite 's' subir, 'd' descer, 'p' pausar, 'a' abortar");
}

void loop() {
  if (Serial.available() > 0) {
    char comando = Serial.read();

    if (comando == 's') {  // subir
      inverterDirecao(true);
      cicloSubida = true;
      cicloDescida = false;
      aplicaEstado(estado);
    }
    else if (comando == 'd') {  // descer
      inverterDirecao(false);
      cicloSubida = false;
      cicloDescida = true;
      aplicaEstado(estado);
    }
    else if (comando == 'p') {  // parar
      estado = pausaEstado(estado);
      cicloSubida = false;
      cicloDescida = false;
      aplicaEstado(estado);
    }
    else if (comando == 'a') {  // abortar
      estado = pausaEstado(estado);
      aplicaEstado(estado);
      delay(2000);
      if (estado == PRESO_CONTRAIDO) {
          setServoContinuo(180, 180, 180, 2000);
      } 
      estado = ESTADO_RELAXADO;
      cicloSubida = false;
      cicloDescida = false;
      aplicaEstado(estado);
    }
  }

  if (cicloSubida) {
    delay(500); 
    if (estado == PRESO_DISTENDIDO) estado = SOBE_1;
    else if (estado == SOBE_1) estado = PRESO_CONTRAIDO;
    else if (estado == PRESO_CONTRAIDO) estado = SOBE_2;
    else if (estado == SOBE_2) estado = PRESO_DISTENDIDO;
    aplicaEstado(estado);
  }

  if (cicloDescida) {
    delay(500);
    if (estado == PRESO_DISTENDIDO) estado = DESCE_1;
    else if (estado == DESCE_1) estado = PRESO_CONTRAIDO;
    else if (estado == PRESO_CONTRAIDO) estado = DESCE_2;
    else if (estado == DESCE_2) estado = PRESO_DISTENDIDO;
    aplicaEstado(estado);
  }
}
